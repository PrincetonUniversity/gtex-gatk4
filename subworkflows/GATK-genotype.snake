import os
from scripts.split_bed import count_chunks

chroms = list(range(1,23))
if "subw_outputs_dict" in locals() and "ids" in locals():
    recal = [paths['vqsr_script']]
    if all([os.path.exists(f) for f in recal]):
        subw_outputs_dict['GATK-genotype'] = [paths['vqsr_vcf']]
    else:
        subw_outputs_dict['GATK-genotype'] = recal

# determine number of chunks for each chromosome
chunks = {}
for chrom in chroms:
    chunks[str(chrom)] = count_chunks(paths['chrom_intervals']\
                                    .format(chromosome=chrom),
                                 config['gatk']['GenotypeGVCFs']['chunk_size'])

rule haplotype_caller:
    input:
        bam=paths['recal_bam'],
        interval=paths['chrom_intervals']

    output:
        vcf=paths['haplotype_raw'],
        idx=paths['haplotype_idx']

    singularity:
        paths['gatk-container']

    group: 'haplotype'

    shell:
        'gatk --java-options "-Xmx{config[gatk][HaplotypeCaller][memory]}" '
            'HaplotypeCaller  '
            '--verbosity {config[gatk][verbosity]} '
            '-R {paths[ref_genome]} '
            '-I {input.bam} '
            '-L {input.interval} '
            '-O {output.vcf} '
            '{config[gatk][HaplotypeCaller][options]}'

rule name_map:
    input:
        lambda wildcards: expand(paths['haplotype_raw']\
                                 .replace('{chromosome}', \
                                          wildcards.chromosome),
                                 id=ids),
        lambda wildcards: expand(paths['haplotype_idx']\
                                 .replace('{chromosome}', \
                                          wildcards.chromosome),
                                 id=ids)

    output:
        temp(paths['name_map'])

    group: 'haplotype'

    run:
        import glob
        import os
        with open(output[0], 'w') as outfile:
            # get variants in output
            variant_dir = os.path.split(paths['haplotype_raw'])[0]
            variant_dir = variant_dir.replace('{chromosome}', wildcards.chromosome)
            # get variants in 1000 genomes
            supp_dir = paths['1000G_dir'].replace('{chromosome}', wildcards.chromosome)

            split_chars = ['.', '_']
            for i, d in enumerate([variant_dir, supp_dir]):
                for f in glob.glob(d + "/*.g.vcf"):
                    sample = os.path.split(f)[1]
                    sample = sample[:sample.find(split_chars[i])]  # remove after first .
                    outfile.write(
                        "{}\t{}\n".format(
                            sample,
                            f))

rule db_import:
    input:
        paths['name_map']

    output:
        temp(directory(paths['genomics_db']))

    singularity:
        paths['gatk-container']

    shell:
        'gatk --java-options "-Xmx{config[gatk][GenomicsDBImport][memory]}" '
            'GenomicsDBImport  '
            '--verbosity {config[gatk][verbosity]} '
            '-L {wildcards.chromosome} '
            '--sample-name-map {input} '
            '--genomicsdb-workspace-path {output} '
            '{config[gatk][GenomicsDBImport][options]} '

rule split_chunk:
    input:
        region=paths['chrom_intervals']

    output:
        temp(paths['genotype_chunk'])

    group: 'genotype_gvcf'

    shell:
        'python {paths[base]}/scripts/split_bed.py '
            '{input.region} '
            '{config[gatk][GenotypeGVCFs][chunk_size]} '
            '{wildcards.chunk} '
            '> {output} '


rule genotype_gvcf:
    input:
        db=paths['genomics_db'],
        intervals=paths['genotype_chunk']

    output:
        vcf=temp(paths['genotype_gvcf_chunk']),
        tbi=temp(paths['genotype_tbi_chunk'])

    singularity:
        paths['gatk-container']

    group: 'genotype_gvcf'

    shell:
        'gatk --java-options "-Xmx{config[gatk][GenotypeGVCFs][memory]}" '
            'GenotypeGVCFs '
            '--verbosity {config[gatk][verbosity]} '
            '-R {paths[ref_genome]} '
            '-L {input.intervals} '
            '--variant gendb://{input.db} '
            '-O {output.vcf} '
            '{config[gatk][GenotypeGVCFs][options]} '
            '2>/dev/null'

def get_chromosome_chunks(wildcards):
    return {"gvcfs": expand(paths['genotype_gvcf_chunk'],
                            chromosome=wildcards.chromosome,
                            chunk=range(chunks[wildcards.chromosome])),
            "idxs": expand(paths['genotype_tbi_chunk'],
                            chromosome=wildcards.chromosome,
                            chunk=range(chunks[wildcards.chromosome]))}

rule vcf_list_chunks:
    input:
        unpack(get_chromosome_chunks),

    output:
        vcfList=temp(paths['genotype_list'])

    group: 'merge_genotype_gvcf'

    run:
        with open(output[0], 'w') as writer:
            for f in input.gvcfs:
                writer.write(f + "\n")
    
rule merge_chunk_gvcf:
    input:
        unpack(get_chromosome_chunks),
        vcfList=paths['genotype_list']

    output:
        vcf=paths['genotype_gvcf'],
        tbi=paths['genotype_tbi']

    singularity:
        paths['gatk-container']

    group: 'merge_genotype_gvcf'

    shell:
        'gatk --java-options "-Xmx{config[gatk][MergeVcfs][memory]}" '
            'MergeVcfs '
            '--VERBOSITY {config[gatk][verbosity]} '
            '--INPUT {input.vcfList} '
            '--OUTPUT {output.vcf} '

def get_chromosomes(wildcards):
    return {"gvcfs": expand(paths['genotype_gvcf'],
                            chromosome=chroms),
            "idxs": expand(paths['genotype_tbi'],
                            chromosome=chroms)}

rule vcf_list_chroms:
    input:
        unpack(get_chromosomes),

    output:
        vcfList=temp(paths['chromosome_list'])

    group: 'merge_genotype_gvcf'

    run:
        with open(output[0], 'w') as writer:
            for f in input.gvcfs:
                writer.write(f + "\n")
    
rule merge_chromosome_gvcf:
    input:
        unpack(get_chromosomes),
        vcfList=paths['chromosome_list']

    output:
        vcf=paths['vqsr_merged_vcf'],

    singularity:
        paths['gatk-container']

    group: 'merge_genotype_gvcf'

    shell:
        'gatk --java-options "-Xmx{config[gatk][MergeVcfs][memory]}" '
            'MergeVcfs '
            '--VERBOSITY {config[gatk][verbosity]} '
            '--INPUT {input.vcfList} '
            '--OUTPUT {output.vcf} '

def get_variant_options(wildcards):
    return config['gatk']['VariantRecalibrator']['options'].format(
            hapmap=paths['ref_hapmap'],
            omni=paths['ref_omni'],
            OKG=paths['ref_1000g'],
            dbsnp=paths['ref_dbsnp'])

rule variant_recal:
    input:
        ref=paths['ref_genome'],
        vcf=paths['vqsr_merged_vcf'],

    output:
        recal=paths['vqsr_recal'],
        tranche=paths['vqsr_tranche'],
        rscript=paths['vqsr_script']

    params:
        get_variant_options

    singularity:
        paths['gatk-container']

    shell:
        'gatk --java-options "-Xmx{config[gatk][VariantRecalibrator][memory]}" '
            'VariantRecalibrator '
            '--verbosity {config[gatk][verbosity]} '
            '-R {input.ref} '
            '-V {input.vcf} '
            '--output {output.recal} '
            '--tranches-file {output.tranche} '
            '--rscript-file {output.rscript} '
            '{params}'

rule apply_vqsr:
    input:
        ref=paths['ref_genome'],
        vcf=paths['vqsr_merged_vcf'],
        recal=paths['vqsr_recal'],
        tranche=paths['vqsr_tranche'],

    output:
        vcf=paths['vqsr_vcf']

    singularity:
        paths['gatk-container']

    shell:
        'gatk --java-options "-Xmx{config[gatk][ApplyVQSR][memory]}" '
            'ApplyVQSR '
            '--verbosity {config[gatk][verbosity]} '
            '-R {input.ref} '
            '--tranches-file {input.tranche} '
            '--recal-file {input.recal} '
            '-V {input.vcf} '
            '-O {output.vcf} '
            '{config[gatk][ApplyVQSR][options]}'
